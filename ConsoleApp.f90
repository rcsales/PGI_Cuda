!
!  ConsoleApp.f90
!
!  Fortran Console Application 
!  Generated by PGI Visual Fortran(R)
!  11/4/2015 10:32:54 PM
!
program driver
 use cudafor
 use cufft
 implicit none
 complex, allocatable,dimension(:,:,:), pinned :: A , B
 complex, allocatable,dimension(:,:,:), device :: A_d, B_d
 real:: elapsed_time,scale
 integer, parameter :: num_streams=8
 integer:: nxx, nyy, nomega, plan, stream(num_streams)
 integer :: clock_start,clock_end,clock_rate, istat, ifr, i,j, ind
 logical:: plog
 nxx=512; nyy=512 ; nomega=196
 ! Initialize FFT plan
 call cufftPlan2d(plan,nyy,nxx,CUFFT_C2C)
 do i = 1,num_streams
 istat= cudaStreamCreate(stream(i))
 end do
 ! Find the clock rate
 call SYSTEM_CLOCK(COUNT_RATE=clock_rate)
 allocate(A(nxx,nyy,nomega),B(nxx,nyy,nomega))
 allocate(A_d(nxx,nyy,num_streams),B_d(nxx,nyy,num_streams))
 istat=cudaThreadSynchronize()
 call SYSTEM_CLOCK(COUNT=clock_start) ! Start timing
 scale =1./(nxx*nyy)
 do ifr=1,nomega
 ind = mod(ifr,num_streams)+1
 ! Send data to GPU
 istat= cudaMemcpyAsync(A_d(1,1,ind),A(1,1,ifr),nxx*nyy, stream(ind))
 istat= cudaMemcpyAsync(B_d(1,1,ind),B(1,1,ifr),nxx*nyy, stream(ind))
 ! Execute FFTs on GPU
 call cufftSetStream(plan,stream(ind))
 call cufftExecC2C(plan ,A_d(1,1,ind),A_d(1,1,ind),CUFFT_FORWARD)
 call cufftExecC2C(plan ,B_d(1,1,ind),B_d(1,1,ind),CUFFT_FORWARD)
 ! Convolution and scaling of the arrays
 !$cuf kernel do(2) <<<*,*,stream=stream(ind)>>>
 do j=1,nyy
 do i=1,nxx
 B_d(i,j,ind)= A_d(i,j,ind)*B_d(i,j,ind)*scale
 end do
 end do
 ! Execute FFTs on GPU
 call cufftExecC2C(plan ,B_d(1,1,ind),B_d(1,1,ind),CUFFT_INVERSE)
 ! Copy results back
 istat=cudaMemcpyAsync( B(1,1,ifr),B_d(1,1,ind),nxx*nyy, stream=stream(ind))
 end do
 istat=cudaThreadSynchronize()
 call SYSTEM_CLOCK(COUNT=clock_end) ! End timing
 print *,"Elapsed time :", REAL(clock_end-clock_start)/REAL(clock_rate)
 deallocate(A,B,A_d,B_d)
end program